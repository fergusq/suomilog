# Suomilog
# Copyright (C) 2026 Iikka Hauhio
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.

.ROOT ::= .NP{+sg,+nom} -> $1
.ROOT ::= .NP{+sg,+gen} -> $1
.ROOT ::= .NP{+pl,+nom} -> $1
.ROOT ::= .NP{+pl,+gen} -> $1
.ROOT ::= .NP-CASE -> $1

.NP-CASE ::= .NP{+sg,+par} -> $1
.NP-CASE ::= .NP{+sg,+ess} -> $1
.NP-CASE ::= .NP{+sg,+abe} -> $1
.NP-CASE ::= .NP{+sg,+tra} -> $1
.NP-CASE ::= .NP{+sg,+ade} -> $1
.NP-CASE ::= .NP{+sg,+abl} -> $1
.NP-CASE ::= .NP{+sg,+all} -> $1
.NP-CASE ::= .NP{+sg,+ine} -> $1
.NP-CASE ::= .NP{+sg,+ela} -> $1
.NP-CASE ::= .NP{+sg,+ill} -> $1

.NP-CASE ::= .NP{+pl,+par} -> $1
.NP-CASE ::= .NP{+pl,+ess} -> $1
.NP-CASE ::= .NP{+pl,+abe} -> $1
.NP-CASE ::= .NP{+pl,+tra} -> $1
.NP-CASE ::= .NP{+pl,+ade} -> $1
.NP-CASE ::= .NP{+pl,+abl} -> $1
.NP-CASE ::= .NP{+pl,+all} -> $1
.NP-CASE ::= .NP{+pl,+ine} -> $1
.NP-CASE ::= .NP{+pl,+ela} -> $1
.NP-CASE ::= .NP{+pl,+ill} -> $1
.NP-CASE ::= .NP{+pl,+ins} -> $1
.NP-CASE ::= .NP{+pl,+com} -> $1


###############################################################################
# Substantiivilausekkeet
###############################################################################

Näillä säännöillä on hierarkia, joka asettaa eri attribuuttien "laskujärjestyksen".

.NP ::= .NP2-PLURAL{$} -> $1
.NP ::= .NP2{$} -> $1

Konjunktioilla erotetut lausekkeet tulkitaan aina monikollisiksi.
(TODO: eikö tai-lausekkeet voi olla yksiköllisiä?)
Vasemmalla puolella oleva !+sg-bitti estää sääntöä laajentumasta, jos sille on annettu ylhäältä +sg-bitti

.NP2-PLURAL{!+sg} ::= .NP2-COMMA-LIST{$} .CONJ .NP2-KEEP-PLURALITY{$} -> $1 ($2) $3

.NP2-COMMA-LIST ::= .NP2-COMMA-LIST{$} , .NP2-LIST-ITEM{$} -> $1 (,) $2
.NP2-COMMA-LIST ::= .NP2-LIST-ITEM{$} -> $1

.NP2-LIST-ITEM ::= .NP2-KEEP-PLURALITY{$} -> $1
.NP2-LIST-ITEM ::= .PREFIX -> ($1) :: 1

Keep plurality -sääntö yliajaa ylhäältä annetut +pl-bitit ja sallii yksikölliset lausekkeet.
\+pl-bitti on miinusbitti, joka poistaa dollarista +pl-bitin jos sellainen on.
Näin konjunktiolausekkeen sisällä voi olla yksiköllisiä lausekkeita,
vaikka konjunktiolauseke kokonaisuudessa on monikollinen.

.NP2-KEEP-PLURALITY ::= .NP2{$,+sg,\+pl} -> $1{+sg}
.NP2-KEEP-PLURALITY ::= .NP2{$,+pl} -> $1{+pl}

Substantiivilausekkeen perässä olevalle obliikville tai infinitiivirakenteelle on asetettu paino,
koska nämä ovat keskimääräistä harvinaisempia rakenteita:

.NP2 ::= .NP3{$} .OBL -> $1 ($2) :: 2
.NP2 ::= .NP3{$} .VP-INF -> $1 ($2) :: 2
.NP2 ::= .NP3{$} -> $1

Alla on säännöt demonstratiivipronomineille ja artikkeleille.
Ne ottavat huomioon tapauksia kuten "ne kolme taloa" tai "kauniit kolme taloa",
jossa on monikollinen artikkeli tai adjektiivi yksiköllisen lukuilmauksen edessä.

.NP3 ::= .ART{$} .NP5{$} -> $1 $2
.NP3 ::= .ART{$,+pl,\+sg} .NP-NUM{$,+sg} -> $1{+pl} $2
.NP3 ::= .ART{$,+pl,\+sg} .AP{$,+pl,\+sg} .NP-NUM{$,+sg} -> $1{+pl} $2{+pl} $3
.NP3 ::= .AP{$,+pl,\+sg} .NP-NUM{$,+sg} -> $1{+pl} $2
.NP3 ::= .NP-NUM{$} -> $1
.NP3 ::= .NP5{$} -> $1

Alla säännöt lukuilmauksille.
Sanalla "yksi" on erikoisasema, koska sen jälkeen substantiivi ei taivu partitiivissa vaan nominatiivissa.

.NP-NUM ::= .NUM{$,+sg,+nom,!yksi:numeral} .NP5{+sg,+par} -> $1 [$2]
.NP-NUM ::= .NUM{$,+sg,+nom,yksi:numeral} .NP5{+sg,+nom} -> $1 $2
.NP-NUM ::= .NUM{$,!+sg+nom} .NP5{$} -> $1 [$2]

Substantiivilauseke määrittämässä erisnimeä (esim ammatit)

.NP5 ::= .NP-ADJ/NOUN .NOUN-PROP{$} -> ($1) $2
.NP5 ::= .NP-ADJ/NOUN{$} -> $1

Adjektiivi- ja genetiiviattribuutit:

.NP-ADJ/NOUN ::= .AP{$} .NP-ADJ/NOUN{$} -> $1 $2
.NP-ADJ/NOUN ::= .GEN .NP-ADJ/NOUN{$} -> ($1) $2
.NP-ADJ/NOUN ::= .ELLIPSED-NOUN{$} -> $1
.NP-ADJ/NOUN ::= .NOUN{$} -> $1

Substantivoituneet adjektiivit:

.NP-ADJ/NOUN ::= .AP{$} -> $1 :: 1

Ellipsit:

.ELLIPSED-NOUN ::= .PREFIX-COMMA-LIST .CONJ .NOUN{$} -> ($1 $2) $3

.PREFIX-COMMA-LIST ::= .PREFIX-COMMA-LIST , .PREFIX -> $1 $2
.PREFIX-COMMA-LIST ::= .PREFIX -> $1

###############################################################################
# Attribuutit
###############################################################################

### Adjektiiviattribuutit ja partisiipit ###

# Listat

.AP ::= .AP-COMMA-LIST{$} .CONJ .AP{$} -> $1 ($2) $3

.AP-COMMA-LIST ::= .AP-COMMA-LIST{$} , .AP{$} -> $1 (,) $2
.AP-COMMA-LIST ::= .AP{$} -> $1

# Adjektiivit

$no-poss = {\+poss1sg,\+poss2sg,\+poss3sg,\+poss1pl,\+poss2pl,\+poss3pl}

.AP ::= .ADJ{$,$no-poss} -> $1
.AP ::= .ADVP .ADJ{$,$no-poss} -> ($1) $2
.AP ::= .ADJ{+sg,+gen} .ADJ{$,$no-poss} -> ($1) $2

# Järjestysluvut

.AP ::= .ORD{$,$no-poss} -> $1

# Agenttipartisiippi

.AP ::= .GEN .VERB{$,+part_ma} -> ($1) $2
.AP ::= .GEN .ADVP .VERB{$,+part_ma} -> ($1 $2) $3

# Kopulapartisiipit ("etsimässä ollut")

.AP ::= .VP-INF .COP-PART{$} -> ($1) $2
.AP ::= .ADVP .VP-INF .COP-PART{$} -> ($1 $2) $3

# Muut partisiipit

.AP ::= .VP-PART{$} -> $1
.AP ::= .ADVP .VP-PART{$} -> ($1) $2

### Genetiiviattribuutit ###

.GEN ::= .NP{+gen} -> $1
.GEN ::= .PRONOUN{+gen} -> $1

### Adverbiaalit ja obliikvit ###

.ADVP ::= .ADVP-COMMA-LIST .CONJ .ADVP -> $1 ($2) $3

.ADVP-COMMA-LIST ::= .ADVP-COMMA-LIST , .ADVP -> $1 (,) $2
.ADVP-COMMA-LIST ::= .ADVP -> $1

.ADVP ::= .ADV -> $1
.ADVP ::= .OBL -> $1

.OBL ::= .NP-CASE -> $1
.OBL ::= .GEN .ADPOS -> ($1) $2

###############################################################################
# Verbirakenteet
###############################################################################

Näiden sääntöjen edessä ei ole valinnaista adverbiaalilausekketta, sillä se on .AP-kohdassa.

.VP-INF ::= .VERB{$,+inf3} -> $1
.VP-INF ::= .OBL .VERB{$,+inf3} -> $1 $2
.VP-INF ::= .OBL .ADVP .VERB{$,+inf3} -> $1 $2 $3

.VP-PART ::= .VERB-PART{$} -> $1
.VP-PART ::= .OBL-LIST .VERB-PART{$} -> ($1) $2

.OBL-LIST ::= .OBL -> $1
.OBL-LIST ::= .OBL-LIST .ADVP -> $1 $2

###############################################################################
# Sanetaso
###############################################################################

$default(..) = {!-rhyphen}

### Perussanaluokat ###

# Substantiivit

.NOUN ::= ..{$,:noun} -> $1
.NOUN ::= ..{$,:verb,+inf4} -> $1

Substantivoidut adjektiivit

.NOUN ::= ..{$,:adjective} -> $1 :: 1

Erikoistapaus: monisaneinen erisnimi

.NOUN ::= .NOUN-PROP{$} -> $1

.NOUN-PROP ::= .NOUN-PROP ..{$,:proper} -> ($1) $2
.NOUN-PROP ::= ..{$,:proper} -> $1
.NOUN-PROP{!+sg} ::= ..{$,:proper-pl,\+pl} -> $1

# Muut

.PRONOUN ::= ..{$,:pronoun} -> $1
.ADJ ::= ..{$,:adjective} -> $1
.ADJ ::= ..{$,:participle} -> $1
.NUM ::= ..{$,:numeral} -> $1
.ORD ::= ..{$,:ordinal} -> $1
.CONJ ::= ..{:conjunction} -> $1
.ADV ::= ..{:adverb} -> $1
.VERB ::= ..{$,:verb} -> $1
.ADPOS ::= ..{$,:adposition} -> $1
.PREFIX ::= ..{$,-rhyphen,\!-rhyphen} -> $1{-rhyphen}

### Verbit ###

# Kopulapartisiipit

.COP-PART ::= ..{$,oleva:participle} -> $1
.COP-PART ::= ..{$,ollut:participle} -> $1

# Partisiipit

Huom! Tämä ei sisällä omistusliitteetöntä agenttipartisiippia, sillä se ei voi esiintyä yksinään vaan tarvitsee eteensä genetiivilausekkeen. Sen sääntö on ylempänä.

.VERB-PART ::= .VERB{$,+part_pres} -> $1
.VERB-PART ::= .VERB{$,+part_past} -> $1
.VERB-PART ::= .VERB{$,+part_maton} -> $1
.VERB-PART ::= .VERB{$,+part_ma,+poss1sg} -> $1
.VERB-PART ::= .VERB{$,+part_ma,+poss2sg} -> $1
.VERB-PART ::= .VERB{$,+part_ma,+poss3sg} -> $1
.VERB-PART ::= .VERB{$,+part_ma,+poss1pl} -> $1
.VERB-PART ::= .VERB{$,+part_ma,+poss2pl} -> $1
.VERB-PART ::= .VERB{$,+part_ma,+poss3pl} -> $1

### Demonstratiivipronominit, artikkelit, kvanttoripronominit sun muut ###

.ART ::= ..{$,tämä:pronoun} -> $1
.ART ::= ..{$,tuo:pronoun} -> $1
.ART ::= ..{$,se:pronoun} -> $1
.ART ::= ..{$,jokin:pronoun} -> $1
.ART ::= ..{$,joku:pronoun} -> $1

TODO: Sanasta kaikki tulee ongelmia. Sen yksikön nominatiivi "kaikki" onkin monikollinen ilmauksissa kuten "kaikki talot" (eikä kaiket talot). Alla oleva koodi ottaa tämän vain osittain huomioon.

.ART ::= ..{$,kaikki:pronoun,!«kaikki»} -> $1
.ART ::= ..{$,kaikki:pronoun,«kaikki»,\+pl} -> $1